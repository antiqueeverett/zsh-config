#!/bin/zsh

#
# Huge thanks to:
# https://gist.github.com/aquaductape/
# https://gist.github.com/LukeSmithxyz/


#
# auto/tab complete:
#
autoload -U compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
_comp_options+=(globdots)


#
# vi mode cursor mode
#
function zle-keymap-select {
    if [[ ${KEYMAP} == vicmd ]] ||
        [[ $1 = 'block' ]]; then
            echo -ne '\e[1 q'
        elif [[ ${KEYMAP} == main ]] ||
            [[ ${KEYMAP} == viins ]] ||
            [[ ${KEYMAP} = '' ]] ||
            [[ $1 = 'beam' ]]; then
                    echo -ne '\e[5 q'
    fi
}

zle -N zle-keymap-select

zle-line-init() {
zle -K viins            # initiate `vi insert` as keymap
                        #   (can be removed if `bindkey -V` has been set elsewhere)
echo -ne "\e[5 q"
}

zle -N zle-line-init
echo -ne '\e[5 q'       # use beam shape cursor on startup.

preexec() {
    echo -ne '\e[5 q';  # use beam shape cursor for each new prompt.
}


#
# change in quotation marks
#
autoload -U select-quoted
zle -N select-quoted
for m in visual viopp; do
    for c in {a,i}{\',\",\`}; do
        bindkey -M $m $c select-quoted
    done
done


#
# change in brackets
#
autoload -U select-bracketed
zle -N select-bracketed
for m in visual viopp; do
    for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
        bindkey -M $m $c select-bracketed
    done
done


#
# sourcing .zshrc
#
function re-source() {
    mv $HOME/.zshrc $HOME/.zshrc_old
    cp $HOME/Repositories/dot-files/.zshrc  $HOME
    source ~/.zshrc
}
alias r='re-source'
